/*
 * Copyright (c) 2014, Imagination Technologies LLC and Imagination
 * Technologies Limited. 
 * 
 * Redistribution and use in source and binary forms, with or without 
 * modification, are permitted under the terms of the MIPS Free To Use 1.0 
 * license that you will have received with this package. If you haven't 
 * received this file, please contact Imagination Technologies or see the 
 * following URL for details.
 * http://codescape-mips-sdk.imgtec.com/license/IMG-free-to-use-on-MIPS-license
 *
 */


/*
 * m32tlb.sx: MIPS32 TLB support functions 
 */

#include "m32tlb.h"

/* 
 * void mips_init_tlb();
 *
 * initialise TLB
 */
LEAF(mips_init_tlb)
XLEAF(mips_tlbinvalall)
	/* first see if we've got a TLB or BAT */
	mfc0	t0,$config,0
	mfc0	t1,$config,1
	
	and	t0,CFG0_MTMASK
	beq	t0,CFG0_MT_BAT,1f	/* we can initialise a BAT */
	bne	t0,CFG0_MT_TLB,9f	/* ...and a TLB, but nothing else */
	
1:	and	a1,t1,CFG1_MMUSMASK	/* get number of entries - 1 */
	srl	a1,CFG1_MMUSSHIFT

	.set	noreorder
	rmfc0	t0,$entryhi		/* save pid */
	mtc0	zero,$entrylo0		/* tlblo0 = invalid */
	mtc0	zero,$entrylo1		/* tlblo1 = invalid */
	mtc0	zero,$pagemask		/* pagemask = 4Kb */
	mtc0	zero,$context		/* context = 0 */
	mtc0	zero,$wired		/* wired = 0 */
	
	/* initialise each entry */
	li	a0,KSEG1_BASE		/* tlbhi  = impossible vpn */
	
1:	rmtc0	a0,$entryhi
	ssnop
	ehb
	
	/* probe for entry matching the vpn */
	tlbp
	ssnop
	ehb
	mfc0	v0,$index
	
	/* if match, bump the vpn value and try again */
	bgez	v0,2f			/* found! */
	nop
	
	/* got a uniqe vpn - write it to tlb - no mcheck possible */
	mtc0	a1,$index		
	subu	a1,1			/* decrement index */
	ssnop
	ehb
	tlbwi
	
	/* continue until whole tlb written */
2:	bgez	a1,1b
	addu	a0,0x2000		/* BDSLOT: inc vpn */

	ehb				/* tlbwi uses $entryhi late */
	rmtc0	t0,$entryhi		/* restore pid */
	.set reorder

9:	jr.hb	ra
END(mips_init_tlb)
